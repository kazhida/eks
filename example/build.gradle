group 'com.abplus'
version '0.0.0'

apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    compile project(':ekspress')
    compile project(':eksgraph')
    compile project(':ekstension')
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.suppressWarnings = true
    kotlinOptions.outputFile = "${projectDir}/node/index.js"
    kotlinOptions.moduleKind = "commonjs" //this is important to properly allow require() import
    //kotlinOptions.verbose = true
}

task assembleWeb(type: Sync) {
    configurations.compile.each { file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/node_modules"

    dependsOn classes
}

assemble.dependsOn assembleWeb

clean.doFirst() {
    delete "${projectDir}/node"
    delete "${projectDir}/node_modules"
}
